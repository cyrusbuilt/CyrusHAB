buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'com.netflix.nebula:gradle-ospackage-plugin:5.1.0'
    }
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'application'

repositories {
    jcenter()
    mavenCentral()
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

dependencies {
    implementation project(':CyrusHABLib')
    implementation 'org.jetbrains:annotations:16.0.2'
    implementation 'com.googlecode.json-simple:json-simple:1.1'
    implementation 'org.apache.commons:commons-lang3:3.8.1'
    implementation 'commons-daemon:commons-daemon:1.0.15'
    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation 'ch.qos.logback:logback-classic:1.2.3'
    implementation 'ch.qos.logback:logback-core:1.2.3'
}

version = '0.1.0'
mainClassName = 'net.cyrusbuilt.cyrushab.daemon.HABDaemon'

jar {
    from('src/main/resources') {
        include 'config.json'
    }

    manifest {
        attributes('Implementation-Title': project.name, 'Implementation-Version': project.version)
    }
}

applicationDistribution.from("src/main/scripts") {
    include 'hab_daemon_launcher.sh'
    into "bin"
}

task runServer(type :Exec) {
    commandLine 'bash', '../run_daemon.sh'
}

apply plugin: 'nebula.deb'

task buildDeb(type: Deb) {
    packageName = project.name.toLowerCase()
    version = project.version
    release = 1

    installUtils file('src/main/scripts/deb/utils.sh')
    preInstall file('src/main/scripts/deb/preInstall.sh')
    preUninstall file('src/main/scripts/deb/preUninstall.sh')

    requires('jsvc')

    into '/opt/' + packageName

    from('src/main/resources') {
        into 'config'
    }

    from (buildDir.absolutePath + "/install/" + project.name + "/lib") {
        into 'lib'
    }

    from('src/main/scripts') {
        include 'hab_daemon_launcher.sh'
        into 'bin'
    }
}